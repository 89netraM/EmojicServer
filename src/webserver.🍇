📦 sockets 🏠

📗
	A configurable server.
📗
🐇 🕸️ 🍇
	🖍🆕 port🔢
	🖍🆕 threadCount🔢 ⬅ 32

	🆕 🍼port🔢 🍇🍉
	🆕 🍼port🔢 🍼threadCount🔢 🍇🍉

	📗
		Run the server on the initialized port and with the specified responder
		function.
	📗
	❗ 🏃 🎍🥡responder🍇📥➡📤🍉 🚧🚧🔸↕ 🍇
		💭 Instantiate and start a TCP listener at the specified port
		🔺 🆕🏄port❗ ➡ server

		💭 Listens on a number of threads
		🆕🍨🐚🧵🍆❗ ➡ 🖍🆕 threads
		🔂 i 🆕⏩0 threadCount❗ 🍇
			🐻threads 🆕🧵🍇🎍🥡
				💭 Wait for new connections
				🔁 👍 🍇
					💭 Wait for a connecting client
					🆗 clientSocket 🙋server❗ 🍇
						🕳👇clientSocket responder❗
					🍉
					🙅 error 🍇
						😀🔤An error occured when connecting to a client: 🧲💬error❗🧲🔤❗
					🍉
				🍉
			🍉❗❗
		🍉

		💭 Wait for all threads to exit (psst, it'll never happen)
		🔂 thread threads 🍇
			🛂thread❗
		🍉
	🍉

	🔒 ❗ 🕳 socket📞 responder🍇📥➡📤🍉 🍇
		👍 ➡ 🖍🆕 reading
		🆕🔠❗ ➡ sb

		💭 Read all the data from the client
		🔁 reading 🍇
			🆗 data 👂socket 1024❗ 🍇
				↪ 🔡data❗ ➡ text 🍇
					🐻sb text❗
				🍉
				↪ 📏data❓ ◀ 1024 🍇
					👎 ➡ 🖍 reading
				🍉
			🍉
			🙅 _ 🍇
				👎 ➡ 🖍 reading
			🍉
		🍉

		💭 Parse the incoming request
		🆗 request 🆕📥🔡sb❗❗ 🍇
			💭 Query the responder for a response
			⁉responder request❗ ➡ response

			💭 Send the response to the client
			🆗 💬socket 📇🔡response❗❗❗ 🍇
				💭 Succesfully sent the response
			🍉
			🙅 error 🍇
				😀🔤An error occured when sending the response to the client: 🧲💬error❗🧲🔤❗
			🍉
		🍉
		🙅 error 🍇
			😀🔤An error occured when the request from the client: 🧲💬error❗🧲🔤❗
		🍉

		💭 Close the client socket
		🚪socket❗
	🍉
🍉
