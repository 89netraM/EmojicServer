📜 🔤webserver.🍇🔤
📜 🔤request.🍇🔤
📜 🔤response.🍇🔤
📜 🔤route.🍇🔤
📜 🔤staticRoute.🍇🔤
📜 🔤methodPathRoute.🍇🔤
📜 🔤filter.🍇🔤
📜 🔤functionFilter.🍇🔤
📜 🔤postProcessor.🍇🔤
📜 🔤functionPostProcessor.🍇🔤
📜 🔤fetch.🍇🔤

🏁 🍇
	🆕🕸️3000❗ ➡ server
	🐻server 🆕📁🔤wwwroot🔤❗❗
	🐐server 🔤/digIT🔤 🍇🎍🥡 request📥 ➡ 📤
		💭 Respondes with the digIT website
		🆗 req 💨🐇🚲 🔤http://digit.chalmers.it🔤❗ 🍇
			🆗 res 🛂req❗ 🍇
				↩ res
			🍉
			🙅 _ 🍇 🍉
		🍉
		🙅 _ 🍇 🍉
		↩ 👿🐇📤❗
	🍉❗
	🔗server 🍇🎍🥡 request📥 ➡ 🍬📤
		💭 Disallow unauthorized API calls
		↪ 🎼🛤request❓ 🔤/api🔤❗ 🍇
			↪ 🎧request 🔤Authorization🔤❓ ➡ auth 🍇
				↪ ❎auth 🙌 🔤Password123🔤❗ 🍇
					↩ 👮🐇📤❗
				🍉
			🍉
			🙅 🍇
				↩ 👮🐇📤❗
			🍉
		🍉
		↩ 🤷
	🍉❗
	🐐server 🔤/api/time🔤 🍇🎍🥡 request📥 ➡ 📤
		💭 Respondes with the current time
		↩ 💻🐇📤 🔤{"time":🧲🕰🐇💻❗🧲}🔤❗
	🍉❗
	🕒server 🍇🎍🥡 request📥 response📤
		💭 Caches API repsonses
		↪ 🎼🛤request❓ 🔤/api🔤❗ 🍇
			🔤max-age=5🔤 ➡ 🎧response 🔤Cache-Control🔤❗
		🍉
	🍉❗
	🍺🏃server❗
🍉
